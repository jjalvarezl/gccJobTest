#
# Notes:
#
# Remove all of the tags below except for the last one when creating new
# development branch.
#
# Once a symbol has been included in a stable release of wxWidgets its version
# cannot be changed.
#
# With each new release a section should be added containing any new symbols,
# for example:
#
#   # public symbols added in release WXU_3.0.2 (please keep in alphabetical order):
#   WXU_3.0.2 {
#           *wxChoice*GetCurrentSelection*;
#   };
#
# If a symbols should have been added in this way, but is forgotten then it
# cannot be added in the next release. This is because it has already been
# released with the generic branch version due to the final wildcard below,
# and once released its version cannot be changed.


# public symbols added in 3.0.4 (please keep in alphabetical order):
WXU_3.0.4 {
    *wxDirButton*DoApplyWidgetStyle*;
    *wxFileButton*DoApplyWidgetStyle*;
    *wxRibbonPage*ShowScrollButtons1*;
    *wxWindow*GTKApplyCssStyle*;
};

# public symbols added in 3.0.3 (please keep in alphabetical order):
WXU_3.0.3 {
    *wxDataViewRenderer*FinishEditing*;
    *wxFloatProperty*DoGetAttribute*;
};

# public symbols added in 3.0.2 (please keep in alphabetical order):
WXU_3.0.2 {
	*wxGenericListCtrl*EndEditLabel*;
};


# public symbols added in 3.0.1 (please keep in alphabetical order):
WXU_3.0.1 {
        *wxDataViewListCtrl*ClearColumns*;
        *wxDataViewListStore*ClearColumns*;
        *wxInfoBarGeneric*SetForegroundColour*;
        *wxTDIChildFrame*DoMoveWindow*;
};

# symbols available since the beginning of this branch are only given
# generic branch tag (don't remove this!):

WXU_3.0 {
        # Explicitly mention this one as otherwise it would be caught by
        # wxDataViewRenderer*FinishEditing wildcard in 3.0.3 above.
        *wxDataViewRendererBase*FinishEditing*;
        *;
};
